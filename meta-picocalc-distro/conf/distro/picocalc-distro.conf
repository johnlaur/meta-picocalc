require conf/distro/poky.conf
require conf/distro/include/gcsections.inc

DISTRO = "picocalc-distro"
DISTRO_NAME = "Picocalc (based on Poky Tiny)"
DISTROOVERRIDES = "poky:picocalc-distro"
TCLIBC = "musl"

# We can use packagegroup-core-boot, but in the future we may need a new packagegroup-core-tiny
#POKY_DEFAULT_EXTRA_RDEPENDS += "packagegroup-core-boot"
# Drop kernel-module-af-packet from RRECOMMENDS
POKY_DEFAULT_EXTRA_RRECOMMENDS = ""

# FIXME: what should we do with this?
TCLIBCAPPEND = ""

DISTRO_FEATURES = "ipv4 usbhost usbgadget wifi overlayfs rauc nls keyboard"

DISTRO_FEATURES:class-native = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"
DISTRO_FEATURES:class-nativesdk = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"

# We try systemd to see how well it runs
DISTRO_FEATURES:append = " systemd usrmerge"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"
# Disabling pulseaudio and sysvinit
DISTRO_FEATURES_BACKFILL_CONSIDERED += "pulseaudio sysvinit gobject-introspection-data ldconfig"
INIT_MANAGER ?= "systemd"
# POKY_INIT_MANAGER:poky-tiny = "mdev-busybox"

IMAGE_FSTYPES = "wic.gz tar.gz"

# Drop v86d from qemu dependency list (we support serial)
# Drop grub from meta-intel BSPs
# FIXME: A different mechanism is needed here. We could define -tiny
#        variants of all compatible machines, but that leads to a lot
#        more machine configs to maintain long term.
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ""

# The mtrace script included by eglibc is a perl script. This means the system
# will build perl in case this package is installed. Since we don't care about
# this script for the purposes of tiny, remove the dependency from here.
RDEPENDS:${PN}-mtrace:pn-eglibc = ""

# Disable python usage in opkg-utils since it won't build with tiny config
PACKAGECONFIG:remove:pn-opkg-utils = "python"

# Enable wifi for connman
# PACKAGECONFIG:pn-connman = "wispr client nftables wifi wpa-supplicant iwd"
